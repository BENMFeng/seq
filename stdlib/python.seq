type PyObject(p: ptr[byte]):
    def incref(self: PyObject):
        cimport seq_py_incref(ptr[byte])
        seq_py_incref(self.p)

    def decref(self: PyObject):
        cimport seq_py_decref(ptr[byte])
        seq_py_decref(self.p)

    def call(self: PyObject, args):
        cimport seq_py_call(ptr[byte], ptr[byte]) -> ptr[byte]
        args_py: PyObject = args.__to_py__()
        return PyObject(seq_py_call(self.p, args_py.p))

    def call_void(self: PyObject):
        cimport seq_py_call(ptr[byte], ptr[byte]) -> ptr[byte]
        return PyObject(seq_py_call(self.p, ptr[byte]()))

    def __getitem__(self: PyObject, name: str):
        cimport seq_py_get_attr(ptr[byte], ptr[byte]) -> ptr[byte]
        return PyObject(seq_py_get_attr(self.p, name.c_str()))

    def __setitem__(self: PyObject, name: str, val: PyObject):
        cimport seq_py_set_attr(ptr[byte], ptr[byte], ptr[byte])
        seq_py_set_attr(self.p, name.c_str(), val.p)

    def __to_py__(self: PyObject):
        return self

class PyException:
    msg: str
    pytype: str

def import_module(name: str):
    cimport seq_py_import(ptr[byte]) -> ptr[byte]
    return PyObject(seq_py_import(name.c_str()))

def none():
    cimport seq_py_none() -> ptr[byte]
    return PyObject(seq_py_none())


# conversions

def py(x) -> PyObject:
    return x.__to_py__()

def get[T](x: PyObject) -> T:
    return T.__from_py__(x)

extend int:
    def __to_py__(self: int):
        cimport seq_int_to_py(int) -> ptr[byte]
        return PyObject(seq_int_to_py(self))

    def __from_py__(n: PyObject):
        cimport seq_int_from_py(ptr[byte]) -> int
        return seq_int_from_py(n.p)

extend float:
    def __to_py__(self: float):
        cimport seq_float_to_py(float) -> ptr[byte]
        return PyObject(seq_float_to_py(self))

    def __from_py__(f: PyObject):
        cimport seq_float_from_py(ptr[byte]) -> float
        return seq_float_from_py(f.p)

extend bool:
    def __to_py__(self: bool):
        cimport seq_bool_to_py(bool) -> ptr[byte]
        return PyObject(seq_bool_to_py(self))

    def __from_py__(b: PyObject):
        cimport seq_bool_from_py(ptr[byte]) -> bool
        return seq_bool_from_py(b.p)

extend byte:
    def __to_py__(self: byte):
        cimport seq_byte_to_py(byte) -> ptr[byte]
        return PyObject(seq_byte_to_py(self))

    def __from_py__(c: PyObject):
        cimport seq_byte_from_py(ptr[byte]) -> byte
        return seq_byte_from_py(c.p)

extend str:
    def __to_py__(self: str):
        cimport seq_str_to_py(str) -> ptr[byte]
        return PyObject(seq_str_to_py(self))

    def __from_py__(s: PyObject):
        cimport seq_str_from_py(ptr[byte]) -> str
        return seq_str_from_py(s.p)

extend seq:
    def __to_py__(self: seq):
        cimport seq_str_to_py(seq) -> ptr[byte]
        return PyObject(seq_str_to_py(self))

    def __from_py__(s: PyObject):
        cimport seq_str_from_py(ptr[byte]) -> seq
        return seq_str_from_py(s.p)
